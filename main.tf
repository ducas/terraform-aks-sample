terraform {}

provider "external" {}
provider "azurerm" {}

variable "resource_group_name" {}
variable "resource_group_location" {}
variable "service_principal_client_id" {}
variable "service_principal_client_secret" {}
variable "cluster_name" {}
variable "kubernetes_version" {}
variable "default_node_count" {}
variable "node_vm_size" {}
variable "max_pods" {}
variable "vnet_subnet_id" {}
variable "log_analytics_workspace_id" {}

# Get the number of nodes in the cluster (default to variable).
data "external" "aks_cluster" {
  program = ["pwsh", "Get-AksNodeCount.ps1"]

  query = {
    cluster_name        = "${var.cluster_name}"
    resource_group_name = "${var.resource_group_name}"
    default_node_count  = "${var.default_node_count}"
  }
}

data "azurerm_client_config" "current" {}

locals {
  generated_resource_group = "MC_${var.resource_group_name}_${var.cluster_name}_${var.resource_group_location}"
}

resource "azurerm_kubernetes_cluster" "aks_cluster" {
  name                = "${var.cluster_name}"
  location            = "${var.resource_group_location}"
  resource_group_name = "${var.resource_group_name}"
  dns_prefix          = "${var.cluster_name}"
  kubernetes_version  = "${var.kubernetes_version}"

  agent_pool_profile {
    name            = "default"
    count           = "${data.external.aks_cluster.result.node_count}"
    vm_size         = "${var.node_vm_size}"
    os_type         = "Linux"
    os_disk_size_gb = 30
    vnet_subnet_id  = "${var.vnet_subnet_id}"
    max_pods        = "${var.max_pods}"
  }

  addon_profile {
    oms_agent {
      enabled                    = true
      log_analytics_workspace_id = "${var.log_analytics_workspace_id}"
    }
  }

  service_principal {
    client_id     = "${var.service_principal_client_id}"
    client_secret = "${var.service_principal_client_secret}"
  }

  network_profile {
    network_plugin = "kubenet"
  }

  lifecycle {
    prevent_destroy = false
  }
}

# Service principal permissions - https://docs.microsoft.com/en-us/azure/aks/kubernetes-service-principal

# Allow the AKS service principal to add resources to the subnet.
resource "azurerm_role_assignment" "service_principal_subnet_contributor" {
  scope                = "${var.vnet_subnet_id}"
  role_definition_name = "Contributor"
  principal_id         = "${var.service_principal_client_id}"
}

# Allow the AKS service principal to modify the cluster (e.g. autoscaler).
resource "azurerm_role_assignment" "service_principal_cluster_contributor" {
  scope                = "${azurerm_kubernetes_cluster.aks_cluster.id}"
  role_definition_name = "Contributor"
  principal_id         = "${var.service_principal_client_id}"
}

# Allow the AKS service principal to contribute to the resource group generated by AKS.
resource "azurerm_role_assignment" "service_principal_newrg_contributor" {
  scope                = "${local.generated_resource_group}"
  role_definition_name = "Contributor"
  principal_id         = "${var.service_principal_client_id}"
}

# Allow the automation account to contribute to the resource group generated by AKS.
resource "azurerm_role_assignment" "client_newrg_contributor" {
  scope                = "${local.generated_resource_group}"
  role_definition_name = "Contributor"
  principal_id         = "${data.azurerm_client_config.current.client_id}"
}
